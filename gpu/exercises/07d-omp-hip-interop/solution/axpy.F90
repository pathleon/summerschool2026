! SPDX-FileCopyrightText: 2025 CSC - IT Center for Science Ltd. <www.csc.fi>
!
! SPDX-License-Identifier: MIT

#include "helper_functions.F90"
#include "hipblas_bindings.F90"

program axpy
  use, intrinsic :: iso_c_binding
  use helper_functions
  use hipblas_bindings
  implicit none
  character(len=32) :: arg
  integer :: i, n
  real(8) :: alpha, frac
  real(8), allocatable :: x(:), y(:)
  type(c_ptr) :: handle
  integer(c_int) :: status

  ! Array size
  n = 102400
  call get_command_argument(1, arg)
  if (len_trim(arg) > 0) then
    read(arg, *) n
  end if
  print '(A, I0)', "Array size n = ", n

  ! Create handle for hipblas
  call hipblasCreate(handle)
  call hipblasSetPointerMode(handle, HIPBLAS_POINTER_MODE_HOST)

  allocate(x(n), y(n))

  ! Initialization
  alpha = 3.0d0
  do i = 1, n
    frac = 1.0d0 / real(n - 1, kind=8)
    x(i) = real(i - 1, kind=8) * frac
    y(i) = real(i - 1, kind=8) * frac * 100.0d0
  end do

  ! Print input values
  print '(A)', "Input:"
  print '(A, F8.4)', "a = ", alpha
  call print_array("x", x)
  call print_array("y", y)

  ! Calculate axpy
  !$omp target data map(to: x(1:n)) map(tofrom: y(1:n))
  !$omp target data use_device_addr(x, y)
  call hipblasDaxpy(handle, n, alpha, x, 1, y, 1)
  !$omp end target data
  !$omp end target data

  ! Print output values
  print '(A)', "Output:"
  call print_array("y", y)

  deallocate(x, y)

  ! Destroy hipblas handle
  call hipblasDestroy(handle)

end program axpy

